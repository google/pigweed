# Copyright 2019 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("//build_overrides/pigweed.gni")

import("$dir_pw_build/python.gni")
import("$dir_pw_docgen/docs.gni")
import("$dir_pw_protobuf_compiler/proto.gni")
import("$dir_pw_third_party/nanopb/nanopb.gni")
import("$dir_pw_unit_test/test.gni")

pw_doc_group("docs") {
  sources = [ "docs.rst" ]
}

pw_test_group("tests") {
  tests = [
    ":nanopb_test",
    ":pwpb_test",
    ":nested_packages_test",
    ":pwpb_no_prefix_test",
    ":prefix_default_test",
  ]
}

pw_test("nanopb_test") {
  deps = [ ":nanopb_test_protos.nanopb" ]
  sources = [ "nanopb_test.cc" ]
  enable_if = dir_pw_third_party_nanopb != ""
}

pw_proto_library("nanopb_test_protos") {
  sources = [ "nanopb_test_protos/nanopb_test.proto" ]
  prefix = "pw_protobuf_compiler"

  if (dir_pw_third_party_nanopb != "") {
    deps = [ "$dir_pw_third_party/nanopb:proto" ]
  }
}

pw_test("pwpb_test") {
  deps = [ ":pwpb_test_protos.pwpb" ]
  sources = [ "pwpb_test.cc" ]
}

pw_proto_library("pwpb_test_protos") {
  sources = [ "pwpb_test_protos/pwpb_test.proto" ]
  inputs = [ "pwpb_test_protos/pwpb_test.pwpb_options" ]
  prefix = "pw_protobuf_compiler"
  deps = [ "$dir_pw_protobuf:common_protos" ]
}

pw_proto_library("test_protos") {
  sources = [
    "pw_protobuf_compiler_protos/nested/more_nesting/test.proto",
    "pw_protobuf_compiler_protos/test.proto",
  ]
}

pw_test("nested_packages_test") {
  deps = [ "pw_nested_packages:aggregate_wrapper.pwpb" ]
  sources = [ "nested_packages_test.cc" ]
}

# This proto library intentionally strips the include path prefix for testing
# purposes.
pw_proto_library("pwpb_no_prefix_test_protos") {
  sources = [ "pwpb_no_prefix_test_protos/pwpb_test_no_prefix.proto" ]
  strip_prefix = "pwpb_no_prefix_test_protos/"
  deps = [ "$dir_pw_protobuf:common_protos" ]
}

pw_test("pwpb_no_prefix_test") {
  sources = [ "pwpb_test_no_prefix.cc" ]
  deps = [ ":pwpb_no_prefix_test_protos.pwpb" ]
}

pw_proto_library("prefix_tests_default_proto") {
  sources = [ "nanopb_test_protos/prefix_tests.proto" ]
  inputs = [ "nanopb_test_protos/prefix_tests.options" ]
  prefix = "pw_protobuf_compiler"
}

pw_test("prefix_default_test") {
  sources = [ "prefix_test.cc" ]
  defines = [ "PW_PROTOBUF_COMPILER_IMPORT=DEFAULT" ]
  deps = [
    ":prefix_tests_default_proto.nanopb",
    "$dir_pw_status",
    "$dir_pw_string",
  ]
  enable_if = dir_pw_third_party_nanopb != ""
}

pw_proto_library("prefix_tests_strip_import_prefix_proto") {
  sources = [ "nanopb_test_protos/prefix_tests.proto" ]
  inputs = [ "nanopb_test_protos/prefix_tests.options" ]
  strip_prefix = "nanopb_test_protos"
}

pw_test("prefix_strip_import_prefix_test") {
  sources = [ "prefix_test.cc" ]
  defines = [ "PW_PROTOBUF_COMPILER_IMPORT=STRIP_IMPORT_PREFIX" ]
  deps = [
    ":prefix_tests_default_proto.nanopb",
    "$dir_pw_status",
    "$dir_pw_string",
  ]
  enable_if = dir_pw_third_party_nanopb != ""
}

pw_proto_library("prefix_tests_import_prefix_proto") {
  sources = [ "nanopb_test_protos/prefix_tests.proto" ]
  inputs = [ "nanopb_test_protos/prefix_tests.options" ]
  strip_prefix = "nanopb_test_protos"
  prefix = "some_prefix"
}

pw_test("prefix_import_prefix_test") {
  sources = [ "prefix_test.cc" ]
  defines = [ "PW_PROTOBUF_COMPILER_IMPORT=IMPORT_PREFIX" ]
  deps = [
    ":prefix_tests_default_proto.nanopb",
    "$dir_pw_status",
    "$dir_pw_string",
  ]
  enable_if = dir_pw_third_party_nanopb != ""
}
