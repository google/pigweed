# Copyright 2023 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("//build_overrides/pi_pico.gni")
import("//build_overrides/pigweed.gni")
import("$dir_pw_build/target_types.gni")
import("$dir_pw_docgen/docs.gni")
import("$dir_pw_unit_test/test.gni")

config("public_includes") {
  include_dirs = [ "public" ]
}

pw_source_set("pw_digital_io_rp2040") {
  public_configs = [ ":public_includes" ]
  public = [ "public/pw_digital_io_rp2040/digital_io.h" ]
  deps = [
    "$PICO_ROOT/src/common/pico_base",
    "$PICO_ROOT/src/common/pico_stdlib",
  ]
  public_deps = [
    "$dir_pw_digital_io",
    "$dir_pw_status",
  ]
  sources = [ "digital_io.cc" ]
  remove_configs = [ "$dir_pw_build:strict_warnings" ]
}

pw_test("digital_io_test") {
  enable_if = pw_build_EXECUTABLE_TARGET_TYPE == "pico_executable"
  sources = [ "digital_io_test.cc" ]
  deps = [ ":pw_digital_io_rp2040" ]
}

pw_doc_group("docs") {
  sources = [ "docs.rst" ]
}

pw_test_group("tests") {
  tests = []

  if (PICO_SRC_DIR != "") {
    tests += [ ":digital_io_test" ]
  }
}
