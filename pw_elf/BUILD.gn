# Copyright 2024 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("//build_overrides/pigweed.gni")

import("$dir_pw_docgen/docs.gni")
import("$dir_pw_unit_test/test.gni")

config("public_include_path") {
  include_dirs = [ "public" ]
  visibility = [ ":*" ]
}

pw_source_set("reader") {
  public_configs = [ ":public_include_path" ]
  sources = [ "reader.cc" ]
  public = [
    "public/pw_elf/internal/elf.h",
    "public/pw_elf/internal/reader_impl.h",
    "public/pw_elf/internal/stream_utils.h",
    "public/pw_elf/reader.h",
  ]
  public_deps = [
    dir_pw_assert,
    dir_pw_bytes,
    dir_pw_log,
    dir_pw_result,
    dir_pw_status,
    dir_pw_stream,
  ]

  # TODO(b/259746255): Remove this when everything compiles with -Wconversion.
  configs = [ "$dir_pw_build:conversion_warnings" ]
}

pw_test("reader_test") {
  enable_if = current_os == "linux"
  sources = [ "reader_test.cc" ]
  defines =
      [ "TEST_ELF_FILE_PATH=\"" +
        rebase_path("$dir_pw_tokenizer/py/elf_reader_test_binary.elf") + "\"" ]
  inputs = [
    # TODO(jrreinhart): Move this file to this module.
    "$dir_pw_tokenizer/py/elf_reader_test_binary.elf",
  ]
  deps = [
    ":reader",
    "$dir_pw_bytes",
    "$dir_pw_stream",
    "$dir_pw_stream:std_file_stream",
  ]
}

pw_test_group("tests") {
  tests = [ ":reader_test" ]
}

pw_doc_group("docs") {
  inputs = [ "examples/reader.cc" ]
  sources = [ "docs.rst" ]
}
