# Copyright 2021 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

load(
    "//pw_build:pigweed.bzl",
    "pw_cc_binary",
    "pw_cc_library",
)

package(default_visibility = ["//visibility:public"])

licenses(["notice"])

pw_cc_library(
    name = "freertos_config",
    hdrs = [
        "config/FreeRTOSConfig.h",
    ],
    includes = ["config/"],
    target_compatible_with = [":freertos_config_cv"],
    deps = ["//third_party/freertos:config_assert"],
)

# Constraint value corresponding to :freertos_config.
#
# If you include this in your platform definition, you will tell Bazel to use
# the :freertos_config defined above when compiling FreeRTOS.  (See
# //third_party/freertos/BUILD.bazel.) If you include it in a target's
# `target_compatible_with`, you will tell Bazel the target can only be built
# for platforms that specify this FreeRTOS config.
constraint_value(
    name = "freertos_config_cv",
    constraint_setting = "//third_party/freertos:freertos_config_setting",
)

# TODO(b/261506064): Additional constraint values for configuring stm32cube
# need to be added here, once constraint settings for stm32cube are defined.
platform(
    name = "platform",
    constraint_values = [
        "@pw_toolchain//constraints/arm_mcpu:cortex-m4",
        "//pw_build/constraints/rtos:freertos",
        ":freertos_config_cv",
        "@freertos//:port_ARM_CM4F",
        "@platforms//cpu:armv7e-m",
    ],
    parents = ["@bazel_embedded//platforms:cortex_m4_fpu"],
)

pw_cc_library(
    name = "pre_init",
    srcs = [
        "boot.cc",
        "vector_table.c",
    ],
    hdrs = [
        "config/stm32f4xx_hal_conf.h",
    ],
    target_compatible_with = [":freertos_config_cv"],
    deps = [
        ":freertos_config",
        "//pw_boot",
        "//pw_boot_cortex_m",
        "//pw_malloc",
        "//pw_preprocessor",
        "//pw_string",
        "//pw_sys_io_stm32cube",
        "//third_party/stm32cube",
        "@freertos",
    ],
)

pw_cc_binary(
    name = "demo",
    srcs = [
        "main.cc",
    ],
    target_compatible_with = [":freertos_config_cv"],
    deps = [
        "//pw_thread:thread",
        "//pw_thread:thread_core",
        "//pw_thread_freertos:thread",
        "@freertos",
    ],
)
