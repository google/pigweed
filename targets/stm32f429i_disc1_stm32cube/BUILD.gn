# Copyright 2021 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("//build_overrides/pigweed.gni")

import("$dir_pw_build/error.gni")
import("$dir_pw_build/target_types.gni")
import("$dir_pw_docgen/docs.gni")
import("$dir_pw_malloc/backend.gni")
import("$dir_pw_toolchain/generate_toolchain.gni")
import("target_toolchains.gni")

config("pw_malloc_active") {
  if (pw_malloc_BACKEND != "") {
    defines = [ "PW_MALLOC_ACTIVE=1" ]
  }
}

if (current_toolchain != default_toolchain) {
  pw_source_set("pre_init") {
    configs = [ ":pw_malloc_active" ]
    deps = [
      "$dir_pw_boot",
      "$dir_pw_boot_cortex_m:armv7m",
      "$dir_pw_malloc",
      "$dir_pw_preprocessor",
      "$dir_pw_string",
      "$dir_pw_sys_io_stm32cube",
      "$dir_pw_third_party/freertos",
      "$dir_pw_third_party/stm32cube",
    ]
    sources = [
      "boot.cc",
      "vector_table.c",
    ]
  }

  config("config_includes") {
    include_dirs = [ "config" ]
  }

  pw_source_set("stm32f4xx_hal_config") {
    public_configs = [ ":config_includes" ]
    public = [ "config/stm32f4xx_hal_conf.h" ]
  }

  pw_source_set("stm32f4xx_freertos_config") {
    public_configs = [ ":config_includes" ]
    public_deps = [ "$dir_pw_third_party/freertos:config_assert" ]
    public = [ "config/FreeRTOSConfig.h" ]
  }

  pw_executable("demo") {
    deps = [
      "$dir_pw_log",
      "$dir_pw_third_party/freertos",
      "$dir_pw_thread:thread",
      "$dir_pw_thread:thread_core",
      "$dir_pw_thread_freertos:thread",
    ]
    sources = [ "main.cc" ]
  }
}

if (dir_pw_third_party_stm32cube_f4 != "") {
  generate_toolchains("target_toolchains") {
    toolchains = pw_target_toolchain_stm32f429i_disc1_list
  }

  group("demo_app") {
    deps = [ ":demo(:stm32f429i_disc1_stm32cube_size_optimized)" ]
  }
} else {
  pw_error("demo_app") {
    message_lines = [
      "Tried to build the STM32Cube demo app, but dir_pw_third_party_stm32cube_f4 is not set.",
      "Download the STM32Cube sources with `pw package install stm32cube_f4` and set dir_pw_third_party_stm32cube_f4 with `gn args out`.",
    ]
  }
}

pw_doc_group("target_docs") {
  sources = [ "target_docs.rst" ]
}
