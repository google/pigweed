# Copyright 2019 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("//build_overrides/pigweed.gni")
import("$dir_pw_chrono/backend.gni")
import("$dir_pw_sync/backend.gni")
import("$dir_pw_thread/backend.gni")
import("$dir_pw_unit_test/test.gni")

# Creates a test target that tests a FakeDispatcher backend using a common
# test suite.
#
# Parameters
#
#   backend (required)
#     [target] The FakeDispatcher backend to test.
template("fake_dispatcher_test") {
  assert(defined(invoker.backend))

  pw_test(target_name) {
    enable_if = pw_chrono_SYSTEM_CLOCK_BACKEND != "" &&
                pw_sync_TIMED_THREAD_NOTIFICATION_BACKEND != "" &&
                pw_thread_THREAD_BACKEND != ""
    deps = [
      "$dir_pw_chrono:system_clock",
      "$dir_pw_containers:vector",
      "$dir_pw_string:to_string",
      "$dir_pw_sync:timed_thread_notification",
      "$dir_pw_thread:thread",
      dir_pw_log,
      invoker.backend,
    ]
    sources = [ "$dir_pw_async/fake_dispatcher_test.cc" ]
  }
}
