# Copyright 2023 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("//build_overrides/pigweed.gni")

import("$dir_pw_build/python.gni")
import("$dir_pw_build/python_action_test.gni")
import("$dir_pw_build/python_dist.gni")
import("$dir_pw_build/python_venv.gni")

pw_python_distribution("test_dist1") {
  packages = []
  generate_setup_cfg = {
    name = "test_dist1"
    version = "0.0.1"
    include_default_pyproject_file = true
    include_extra_files_in_package_data = true
  }
  extra_files = [
    "package_data/empty.csv > test_dist1_data/empty.csv",
    "package_data/empty.csv > test_dist1_data/subdir/empty.csv",
  ]
}

pw_python_action_test("python_package_integration_tests") {
  action = ":test_dist1._build_wheel($pw_build_PYTHON_TOOLCHAIN)"
  deps = [
    ":downstream_project_bundle",
    ":downstream_project_tools",
  ]
  tags = [ "integration" ]
}

# DOCSTAG: [downstream-project-venv]
pw_python_distribution("downstream_project_tools") {
  packages = [
    "$dir_pw_env_setup/py",
    "$dir_pw_console/py",
  ]
  generate_setup_cfg = {
    name = "downstream_project_tools"
    version = "0.0.1"
    include_default_pyproject_file = true
  }
}

pw_python_venv("downstream_tools_build_venv") {
  path = "$root_build_dir/python-venv-downstream-tools-test"
  requirements = []
  constraints =
      [ "$dir_pw_env_setup/py/pw_env_setup/virtualenv_setup/constraint.list" ]
  source_packages = [ ":downstream_project_tools" ]
}

# DOCSTAG: [downstream-project-venv]

pw_python_zip_with_setup("downstream_project_bundle") {
  packages = [ ":downstream_project_tools" ]
  venv = ":downstream_tools_build_venv"
}
