# Copyright 2025 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

load("@rules_rust//rust:defs.bzl", "rust_binary", "rust_library", "rust_test")
load("//pw_build:merge_flags.bzl", "flags_from_dict")
load("//pw_build:pw_linker_script.bzl", "pw_linker_script")
load("//pw_kernel:flags.bzl", "KERNEL_DEVICE_COMMON_FLAGS")
load("//pw_kernel/tooling:system_codegen.bzl", "system_codegen")
load("//pw_kernel/tooling:system_image.bzl", "system_image", "system_image_test")
load("//pw_kernel/tooling/panic_detector:rust_binary_no_panics_test.bzl", "rust_binary_no_panics_test")

platform(
    name = "mps2_an505",
    constraint_values = [
        ":target_mps2_an505",
        "//pw_build/constraints/arm:cortex-m33",
        "@platforms//cpu:armv8-m",
        "@platforms//os:none",
        "//pw_build/constraints/rust:no_std",
    ],
    flags = flags_from_dict(
        KERNEL_DEVICE_COMMON_FLAGS | {
            "//pw_kernel/config:kernel_config": ":config",
            "//pw_kernel/subsys/console:console_backend": "//pw_kernel/subsys/console:console_backend_semihosting",
            "//pw_kernel/target:linker_script": ":linker_script",
        },
    ),
)

constraint_value(
    name = "target_mps2_an505",
    constraint_setting = "//pw_kernel/target:target",
    visibility = ["//visibility:public"],
)

TARGET_COMPATIBLE_WITH = select({
    ":target_mps2_an505": [],
    "//conditions:default": ["@platforms//:incompatible"],
})

system_image(
    name = "kernel_only_demo",
    kernel = ":target",
    platform = ":mps2_an505",
    tags = ["kernel"],
    visibility = ["//visibility:public"],
)

rust_binary_no_panics_test(
    name = "kernel_only_demo_no_panics_test",
    binary = ":kernel_only_demo",
    tags = [
        "kernel",
        "kernel_panic_test",
    ],
)

system_image_test(
    name = "kernel_only_demo_test",
    image = ":kernel_only_demo",
    target_compatible_with = TARGET_COMPATIBLE_WITH,
)

system_image(
    name = "userspace_demo",
    apps = [
        "//pw_kernel/apps/one",
        "//pw_kernel/apps/two",
    ],
    kernel = ":userspace_demo_target",
    platform = ":mps2_an505",
    system_config = ":userspace_demo_config",
    tags = ["kernel"],
    visibility = ["//visibility:public"],
)

rust_binary_no_panics_test(
    name = "userspace_demo_no_panics_test",
    binary = ":userspace_demo",
    tags = ["kernel"],
)

system_image(
    name = "ticker",
    apps = [
        "//pw_kernel/apps/ticker",
    ],
    kernel = ":ticker_target",
    platform = ":mps2_an505",
    system_config = ":ticker_config",
    tags = ["kernel"],
    visibility = ["//visibility:public"],
)

rust_binary_no_panics_test(
    name = "ticker_no_panics_test",
    binary = ":ticker",
    tags = ["kernel"],
)

system_image_test(
    name = "ticker_test",
    image = ":ticker",
    target_compatible_with = TARGET_COMPATIBLE_WITH,
)

SRCS = ["target.rs"]

COMMON_DEPS = [
    ":linker_script",
    "//pw_kernel/kernel",
    "//pw_kernel/arch/arm_cortex_m:arch_arm_cortex_m",
    "//pw_kernel/subsys/console:console_backend_semihosting",
    "//pw_kernel/target:target_common",
    "@rust_crates//:cortex-m",
    "@rust_crates//:cortex-m-rt",
    "@rust_crates//:panic-halt",
]

rust_binary(
    name = "target",
    srcs = SRCS,
    tags = ["kernel"],
    target_compatible_with = TARGET_COMPATIBLE_WITH,
    deps = [
        "//pw_kernel/apps/kernel_only_demo:demo",
        "@rust_crates//:cortex-m-semihosting",
    ] + COMMON_DEPS,
)

filegroup(
    name = "userspace_demo_config",
    srcs = ["userspace_demo.json5"],
)

system_codegen(
    name = "userspace_demo_codegen",
    system_config = ":userspace_demo_config",
)

rust_binary(
    name = "userspace_demo_target",
    srcs = [
        "userspace_demo_target.rs",
        ":userspace_demo_codegen",
    ],
    tags = ["kernel"],
    target_compatible_with = TARGET_COMPATIBLE_WITH,
    deps = [
        "//pw_kernel/userspace",
        "//pw_log/rust:pw_log",
    ] + COMMON_DEPS,
)

filegroup(
    name = "ticker_config",
    srcs = ["ticker.json5"],
)

system_codegen(
    name = "ticker_codegen",
    system_config = ":ticker_config",
)

rust_binary(
    name = "ticker_target",
    srcs = [
        "ticker_target.rs",
        ":ticker_codegen",
    ],
    tags = ["kernel"],
    target_compatible_with = TARGET_COMPATIBLE_WITH,
    deps = [
        "//pw_kernel/userspace",
        "//pw_log/rust:pw_log",
        "@rust_crates//:cortex-m-semihosting",
    ] + COMMON_DEPS,
)

rust_test(
    name = "unittest_runner",
    srcs = SRCS,
    tags = ["kernel"],
    target_compatible_with = TARGET_COMPATIBLE_WITH,
    use_libtest_harness = False,
    deps = COMMON_DEPS + [
        "//pw_kernel/kernel/tests:integration_tests",
        "//pw_kernel/lib/unittest:unittest_core",
        "@pigweed//pw_log/rust:pw_log",
        "@rust_crates//:cortex-m-semihosting",
    ],
)

rust_library(
    name = "config",
    srcs = ["config.rs"],
    crate_name = "kernel_config",
    tags = ["kernel"],
    target_compatible_with = TARGET_COMPATIBLE_WITH,
    deps = [
        "//pw_kernel/config:kernel_config_interface",
    ],
)

pw_linker_script(
    name = "linker_script",
    linker_script = "system.ld",
    tags = ["kernel"],
    target_compatible_with = TARGET_COMPATIBLE_WITH,
)
