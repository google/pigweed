# Copyright 2025 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

load("@rules_rust//rust:defs.bzl", "rust_doc", "rust_library")

package(default_visibility = ["//visibility:public"])

CORTEX_M_DEPS = [
    "@rust_crates//:cortex-m",
    "@rust_crates//:cortex-m-rt",
]

RISCV_DEPS = [
    "@rust_crates//:riscv",
]

rust_library(
    name = "kernel",
    srcs = [
        "interrupt.rs",
        "lib.rs",
        "memory.rs",
        "object.rs",
        "object/buffer.rs",
        "object/channel.rs",
        "object/ticker.rs",
        "panic.rs",
        "scheduler.rs",
        "scheduler/locks.rs",
        "scheduler/thread.rs",
        "scheduler/timer.rs",
        "sync.rs",
        "sync/event.rs",
        "sync/mutex.rs",
        "sync/spinlock.rs",
        "syscall.rs",
        "target.rs",
    ],
    crate_features = ["user_space"] + select({
        "@platforms//cpu:armv8-m": ["arch_arm_cortex_m"],
        "@platforms//cpu:riscv32": ["arch_riscv"],
        "//conditions:default": [
            "arch_host",
            "std_panic_handler",
        ],
    }),
    edition = "2024",
    proc_macro_deps = [
        "@rust_crates//:paste",
    ] + select({
        "@platforms//cpu:armv8-m": ["//pw_kernel/macros:arm_cortex_m_macro"],
        "@platforms//cpu:riscv32": ["//pw_kernel/macros:riscv_macro"],
        "//conditions:default": [],
    }),
    tags = ["kernel"],
    deps = [
        "//pw_kernel/config:kernel_config",
        "//pw_kernel/lib/foreign_box",
        "//pw_kernel/lib/list",
        "//pw_kernel/lib/log_if",
        "//pw_kernel/lib/pw_assert",
        "//pw_kernel/lib/pw_atomic",
        "//pw_kernel/lib/pw_cast",
        "//pw_kernel/lib/regs",
        "//pw_kernel/lib/time",
        "//pw_kernel/syscall:syscall_defs",
        "//pw_log/rust:pw_log",
        "//pw_status/rust:pw_status",
        "@rust_crates//:bitflags",
    ] + select({
        "@platforms//cpu:armv8-m": CORTEX_M_DEPS,
        "@platforms//cpu:riscv32": RISCV_DEPS,
        "//conditions:default": [],
    }),
)

rust_doc(
    name = "kernel_doc",
    crate = ":kernel",
    rustdoc_flags = [
        "--document-private-items",
        "--cfg=feature=\"user_space\"",
    ] + select({
        # Arch feature selection will be removed when arch support moves out of
        # the kernel crate.
        "@platforms//cpu:armv8-m": ["--cfg=feature=\"arch_arm_cortex_m\""],
        "@platforms//cpu:riscv32": ["--cfg=feature=\"arch_riscv\""],
        "//conditions:default": [
            "--cfg=feature=\"arch_host\"",
            "--cfg=feature=\"std_panic_handler\"",
        ],
    }),
    tags = ["kernel"],
)
