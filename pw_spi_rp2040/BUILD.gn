# Copyright 2023 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("//build_overrides/pi_pico.gni")
import("//build_overrides/pigweed.gni")
import("$dir_pw_build/target_types.gni")
import("$dir_pw_docgen/docs.gni")
import("$dir_pw_unit_test/test.gni")

config("default_config") {
  include_dirs = [ "public" ]
}

pw_source_set("pw_spi_rp2040") {
  public_configs = [ ":default_config" ]
  public = [ "public/pw_spi_rp2040/initiator.h" ]
  public_deps = [
    "$PICO_ROOT/src/rp2_common/hardware_spi",
    "$dir_pw_digital_io",
    "$dir_pw_spi:chip_selector_digital_out",
    "$dir_pw_spi:initiator",
    "$dir_pw_status",
  ]
  deps = [
    "$PICO_ROOT/src/common/pico_base",
    "$PICO_ROOT/src/common/pico_stdlib",
    "$dir_pw_log",
  ]
  sources = [ "initiator.cc" ]
  remove_configs = [ "$dir_pw_build:strict_warnings" ]
}

pw_test("initiator_test") {
  enable_if = pw_build_EXECUTABLE_TARGET_TYPE == "pico_executable"
  sources = [ "initiator_test.cc" ]
  deps = [
    ":pw_spi_rp2040",
    "$dir_pw_digital_io_rp2040",
    "$dir_pw_spi:device",
    "$dir_pw_sync:borrow",
    "$dir_pw_sync:mutex",
  ]
}

pw_doc_group("docs") {
  sources = [ "docs.rst" ]
}

pw_test_group("tests") {
  tests = []

  if (PICO_SRC_DIR != "") {
    tests += [ ":initiator_test" ]
  }
}
