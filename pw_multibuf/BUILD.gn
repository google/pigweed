# Copyright 2023 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("//build_overrides/pigweed.gni")

import("$dir_pw_build/target_types.gni")
import("$dir_pw_docgen/docs.gni")
import("$dir_pw_unit_test/test.gni")

config("public_include_path") {
  include_dirs = [ "public" ]
  visibility = [ ":*" ]
}

pw_source_set("chunk") {
  public_configs = [ ":public_include_path" ]
  public = [ "public/pw_multibuf/chunk.h" ]
  sources = [ "chunk.cc" ]
  public_deps = [
    "$dir_pw_sync:mutex",
    dir_pw_assert,
    dir_pw_bytes,
    dir_pw_preprocessor,
    dir_pw_span,
  ]
  deps = [ "$dir_pw_assert:check" ]
}

pw_source_set("test_utils") {
  public_configs = [ ":public_include_path" ]
  public = [ "public/pw_multibuf/internal/test_utils.h" ]
  public_deps = [
    ":chunk",
    "$dir_pw_allocator:allocator_metric_proxy_for_test",
    "$dir_pw_allocator:split_free_list_allocator",
  ]
}

pw_test("chunk_test") {
  deps = [
    ":chunk",
    ":test_utils",
  ]
  sources = [ "chunk_test.cc" ]
}

pw_source_set("pw_multibuf") {
  public_configs = [ ":public_include_path" ]
  public = [ "public/pw_multibuf/multibuf.h" ]
  sources = [ "multibuf.cc" ]
  public_deps = [ ":chunk" ]
}

pw_test("multibuf_test") {
  deps = [
    ":pw_multibuf",
    ":test_utils",
  ]
  sources = [ "multibuf_test.cc" ]
}

pw_test_group("tests") {
  tests = [
    ":chunk_test",
    ":multibuf_test",
  ]
}

pw_doc_group("docs") {
  sources = [ "docs.rst" ]
}
