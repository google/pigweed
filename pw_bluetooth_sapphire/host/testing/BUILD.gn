# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/connectivity/bluetooth/core/bt-host/build/bt_gtest_package.gni")

source_set("testing") {
  testonly = true

  sources = [
    "controller_test.h",
    "inspect.h",
    "inspect_util.cc",
    "inspect_util.h",
    "parse_args.cc",
    "parse_args.h",
    "test_packets.cc",
    "test_packets.h",
  ]

  deps = [ "//third_party/googletest:gtest" ]

  public_deps = [
    ":fake_controller",
    ":mock_controller",
    ":test_helpers",
    "//sdk/lib/inspect/testing/cpp",
    "//src/connectivity/bluetooth/core/bt-host/transport",
    "//src/connectivity/bluetooth/core/bt-host/transport:testing",
    "//src/lib/testing/loop_fixture",
    "//third_party/pigweed/backends/pw_log:printf",
  ]
}

# test_helpers is separate from :testing to fix dependency cycles.
source_set("test_helpers") {
  testonly = true

  sources = [ "test_helpers.h" ]

  deps = [
    "//src/connectivity/bluetooth/core/bt-host/common",
    "//src/connectivity/bluetooth/lib/cpp-string",
    "//third_party/googletest:gtest",
  ]

  public_deps = [ "//third_party/googletest:gmock" ]
}

source_set("tests") {
  testonly = true

  sources = [
    "fake_controller_unittest.cc",
    "fake_dynamic_channel_unittest.cc",
    "fake_l2cap_unittest.cc",
    "fake_sdp_server_unittest.cc",
    "fake_signaling_server_unittest.cc",
    "inspect_util_unittest.cc",
    "parse_args_unittest.cc",
  ]

  deps = [
    ":testing",
    "//src/connectivity/bluetooth/core/bt-host/l2cap:testing",
  ]
}

bt_gtest_package("bt-host-testing-tests") {
  deps = [ ":tests" ]
}

source_set("controller_test_double_base") {
  testonly = true

  sources = [
    "controller_test_double_base.cc",
    "controller_test_double_base.h",
  ]

  public_deps = [
    "//sdk/lib/fit",
    "//src/connectivity/bluetooth/core/bt-host/common",
    "//src/connectivity/bluetooth/core/bt-host/hci",
    "//third_party/pigweed:pw_bluetooth",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-default",
  ]
}

source_set("mock_controller") {
  testonly = true

  sources = [
    "mock_controller.cc",
    "mock_controller.h",
  ]

  deps = [ "//third_party/googletest:gtest" ]

  public_deps = [
    ":controller_test_double_base",
    ":test_helpers",
    "//src/connectivity/bluetooth/core/bt-host/transport",
    "//src/connectivity/bluetooth/core/bt-host/transport:testing",
    "//src/lib/testing/loop_fixture",
    "//third_party/pigweed:pw_async_heap_dispatcher",
  ]
}

# Target that includes Fake HCI emulation support. This should NOT depend on
# gtest.
source_set("fake_controller") {
  testonly = true

  sources = [
    "fake_controller.cc",
    "fake_controller.h",
    "fake_dynamic_channel.cc",
    "fake_dynamic_channel.h",
    "fake_gatt_server.cc",
    "fake_gatt_server.h",
    "fake_l2cap.cc",
    "fake_l2cap.h",
    "fake_peer.cc",
    "fake_peer.h",
    "fake_sdp_server.cc",
    "fake_sdp_server.h",
    "fake_signaling_server.cc",
    "fake_signaling_server.h",
  ]

  public_deps = [
    ":controller_test_double_base",
    ":test_helpers",
    "//sdk/lib/fit",
    "//src/connectivity/bluetooth/core/bt-host/att:definitions",
    "//src/connectivity/bluetooth/core/bt-host/common",
    "//src/connectivity/bluetooth/core/bt-host/gap",
    "//src/connectivity/bluetooth/core/bt-host/gatt:definitions",
    "//src/connectivity/bluetooth/core/bt-host/hci",
    "//src/connectivity/bluetooth/core/bt-host/l2cap:definitions",
    "//src/connectivity/bluetooth/core/bt-host/l2cap:testing",
    "//src/connectivity/bluetooth/core/bt-host/sdp",
    "//third_party/pigweed/src/pw_bluetooth:emboss_hci_group",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-default",
  ]
}

# Main entry point for host library unittests.
source_set("gtest_main") {
  testonly = true

  sources = [ "run_all_unittests.cc" ]

  deps = [
    ":testing",
    "//src/connectivity/bluetooth/core/bt-host/common",
  ]

  public_deps = [ "//third_party/googletest:gtest" ]
}

source_set("fuzzing") {
  testonly = true
  sources = [ "peer_fuzzer.h" ]
  deps = [ "//src/connectivity/bluetooth/core/bt-host/gap" ]
}

source_set("fuzztest_driver") {
  testonly = true
  sources = [ "fuzztest_driver.cc" ]
  deps = [
    "//src/connectivity/bluetooth/core/bt-host/common",
    "//third_party/pigweed/backends/pw_log:printf",
  ]
}
