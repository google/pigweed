# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/connectivity/bluetooth/core/bt-host/build/bt_gtest_package.gni")

group("controllers") {
  public_deps = [
    ":banjo_controller",
    ":fidl_controller",
  ]
}

source_set("helpers") {
  visibility = [ ":*" ]

  sources = [
    "helpers.cc",
    "helpers.h",
  ]

  public_deps = [
    "//third_party/pigweed/src/pw_bluetooth",
    "//zircon/system/ulib/zx",
  ]
}

source_set("banjo_controller") {
  sources = [
    "banjo_controller.cc",
    "banjo_controller.h",
  ]

  deps = [
    ":helpers",
    "//src/connectivity/bluetooth/core/bt-host/common",
    "//src/connectivity/bluetooth/core/bt-host/transport",
    "//src/lib/ddk",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zx",
  ]

  public_deps = [
    "//sdk/banjo/fuchsia.hardware.bt.hci:fuchsia.hardware.bt.hci_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.bt.vendor:fuchsia.hardware.bt.vendor_banjo_cpp",
    "//third_party/pigweed/src/pw_bluetooth",
    "//zircon/system/ulib/async",
  ]
}

source_set("fidl_controller") {
  sources = [
    "fidl_controller.cc",
    "fidl_controller.h",
  ]

  deps = [
    ":helpers",
    "//src/connectivity/bluetooth/core/bt-host/common",
    "//src/connectivity/bluetooth/core/bt-host/transport",
    "//src/lib/ddk",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/zx",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.hardware.bluetooth:fuchsia.hardware.bluetooth_hlcpp",
    "//third_party/pigweed/src/pw_bluetooth",
    "//zircon/system/ulib/async",
  ]
}

source_set("tests") {
  testonly = true

  sources = [
    "banjo_controller_unittest.cc",
    "fidl_controller_unittest.cc",
  ]

  deps = [
    ":controllers",
    "//src/connectivity/bluetooth/core/bt-host/testing",
    "//src/devices/testing/mock-ddk",
    "//src/lib/ddktl",
    "//src/lib/testing/loop_fixture",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
  ]
}

bt_gtest_package("bt-host-controllers-tests") {
  deps = [ ":tests" ]
}
